0 S=0 :REM SCORE
1 X = 20 :REM START X VAL
2 Y = 10 : REM START Y VAL
3 XSP= 0 : REM X SPEED
4 YSP= 0
5 PRINT"{clr}";:POKE53281,9:POKE53280,8 :REM CLEAR AND COLOR
10 FOR I = 1 TO 25 :N=RND(1)*999 :POKE1024+N,88:POKE55296+N,5:NEXTI:REM TREES
11 FOR I = 1 TO 10 :N=RND(1)*919 :POKE1064+N,83:POKE55336+N,2:NEXTI:REM HEARTS
12 FOR I = 0 TO 39 :POKE1024+I,127:POKE55296+I,0:NEXTI:REM BORDER TOP
13 FOR I = 0 TO 39 :POKE1984+I,127:POKE56256+I,0:NEXTI:REM BORDER BOTTOM
14 GOSUB100:X=X+XSP:Y=Y+YSP:GOSUB300 :REM CLEAR MOVE CHECK
15 POKE 1024 +X + 40*Y,81: REM SHOW CIRCLE
16 POKE55296 +X + 40*Y,7
20 GET A$ :REM LISTEN FOR KEY
30 IF A$ = "I" THEN YSP=-1:XSP=0:GOSUB 100
40 IF A$ = "K" THEN YSP=+1:XSP=0:GOSUB 100
50 IF A$ = "J" THEN XSP=-1:YSP=0:GOSUB 100
60 IF A$ = "L" THEN XSP=+1:YSP=0:GOSUB 100
70 IF X < 0 THEN GOTO500
71 IF X > 39THEN GOTO500
72 IF Y < 1 THEN GOTO500
73 IF Y > 23 THEN GOTO500
80 GOTO 14
99 REM SUB CLEARS OLD CIR POSITION
100 POKE 1024 +X + 40*Y,32:REM DEL CIR
110 POKE55296 +X + 40*Y,7:RETURN
299 REM SUB CHEKS FOR COLLISION TREE OR HEART
300 IF PEEK(1024+X+40*Y)= 88THEN 500
302 IF PEEK(1024+X+40*Y)= 83THEN S=S+1
303 IF S =10THEN 600 :
400 RETURN
499 REM END GAME
500 PRINT"{clr}";
510 PRINT "GAME OVER"
520 PRINT "SCORE",S
530 END
600 PRINT"{clr}";
610 PRINT "YOU GOT ALL TH LOVE!!!"
620 PRINT "SCORE",S 
